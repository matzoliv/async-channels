plugins {
    id 'java'
    id 'maven'
    id 'signing'
}

group 'io.github.matzoliv'
version '0.0.1-alpha'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'async-channels'
                packaging 'jar'
                description 'Implementation of Clojure\'s core.async channels for Java & Scala.'
                url 'https://matzoliv.github.io/async-channels'

                scm {
                    connection 'scm:git:https://github.com/matzoliv/async-channels.git'
                    developerConnection 'scm:git:https://github.com/matzoliv/async-channels.git'
                    url 'https://github.com/matzoliv/async-channels'
                }

                licenses {
                    license {
                        name 'Eclipse Public License 1.0'
                        url 'https://opensource.org/licenses/eclipse-1.0.php'
                    }
                }

                developers {
                    developer {
                        id 'omatz'
                        name 'Olivier Matz'
                        email 'omatz@fastmail.com'
                    }
                }
            }
        }
    }
}